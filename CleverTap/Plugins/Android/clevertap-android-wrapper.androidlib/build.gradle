apply plugin: 'com.android.library'

ext {

    libraryName = 'CleverTapAndroidWrapper'
    artifact = 'clevertap-android-wrapper'
    libraryDescription = 'The CleverTap Android Wrapper for Unity SDK'

    mindSdkVersionVal = 21
    compileSdkVersionVal = 36
    targetSdkVersionVal = compileSdkVersionVal

    libraryVersion = "5.5.2"
}

android {
    // Checks if new new build properties exists are needed for 2021.3 (AGP 4.0.1)
    if (project.android.hasProperty("namespace")) {
        namespace "com.clevertap.unity"
    }
    if (project.android.hasProperty('compileSdk')) {
        compileSdk compileSdkVersionVal
    } else {
        compileSdkVersion compileSdkVersionVal
    }

    buildToolsVersion project.getProperty('unity.buildToolsVersion')

    defaultConfig {
        if (project.android.hasProperty('minSdk')) {
            minSdk mindSdkVersionVal
        } else {
            minSdkVersion mindSdkVersionVal
        }
        if (project.android.hasProperty('targetSdk')) {
            targetSdk targetSdkVersionVal
        } else {
            targetSdkVersion targetSdkVersionVal
        }
        versionCode libraryVersion.replaceAll('\\.', '').toInteger()
        versionName libraryVersion
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        main {
            assets.srcDirs = ['assets']
        }
    }
}

dependencies {
    compileOnly fileTree('libs')

    api 'com.clevertap.android:clevertap-android-sdk:7.8.0'
    implementation "androidx.core:core:1.13.0"
    implementation "com.google.firebase:firebase-messaging:24.0.0"
}