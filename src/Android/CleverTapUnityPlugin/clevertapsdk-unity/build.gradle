apply plugin: 'com.android.library'

ext {

    libraryName = 'CleverTapUnityPlugin'
    artifact = 'clevertapsdk-unity'
    libraryDescription = 'The CleverTap Unity SDK'

    mindSdkVersionVal = 19
    compileSdkVersionVal = 33
    targetSdkVersionVal = compileSdkVersionVal
    buildToolsVersionVal = "33.0.0"

    libraryVersion = "2.3.0"
}

android {
    compileSdkVersion compileSdkVersionVal
    buildToolsVersion buildToolsVersionVal

    defaultConfig {
        minSdkVersion mindSdkVersionVal
        targetSdkVersion targetSdkVersionVal

        versionCode libraryVersion.replaceAll('\\.', '').toInteger()
        versionName libraryVersion
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            if (variant.getName().toLowerCase().contains('release')) {
                outputFileName = "${artifact}-${libraryVersion}.aar"
            } else if (variant.getName().toLowerCase().contains('debug')) {
                outputFileName = "${artifact}-${libraryVersion}-${variant.name}.aar"
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // This will add classes from com.unity3d.player to our library which will  allow the ClevertapPlugin.java class to get access to current unity activity and work on it accordingly
    compileOnly fileTree('libs')

    implementation 'com.clevertap.android:clevertap-android-sdk:4.7.4'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.6.0-rc01'
}