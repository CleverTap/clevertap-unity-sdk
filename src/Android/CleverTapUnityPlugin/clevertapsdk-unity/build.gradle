apply plugin: 'com.android.library'

ext {

    libraryName = 'CleverTapUnityPlugin'
    artifact = 'clevertapsdk-unity'
    libraryDescription = 'The CleverTap Unity SDK'

    mindSdkVersionVal = 16
    compileSdkVersionVal = 29
    targetSdkVersionVal = compileSdkVersionVal
    buildToolsVersionVal = "29.0.3"

    libraryVersion = "2.1.0"
}

android {
    compileSdkVersion compileSdkVersionVal
    buildToolsVersion buildToolsVersionVal

    defaultConfig {
        minSdkVersion mindSdkVersionVal
        targetSdkVersion targetSdkVersionVal

        versionCode libraryVersion.replaceAll('\\.', '').toInteger()
        versionName libraryVersion
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            if (variant.getName().toLowerCase().contains('release')) {
                outputFileName = "${artifact}-${libraryVersion}.aar"
            } else if (variant.getName().toLowerCase().contains('debug')) {
                outputFileName = "${artifact}-${libraryVersion}-${variant.name}.aar"
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly fileTree('libs')
    implementation 'com.clevertap.android:clevertap-android-sdk:4.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
}